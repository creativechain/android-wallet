buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'androidsvgdrawable'

android {
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "crea.wallet.lite"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 16
        versionName " 0.2.5"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'org/creativecoin/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/creativecoin/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

repositories {
    maven { url 'http://dl.bintray.com/madcyph3r/maven/' }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://github.com/omadahealth/omada-nexus/raw/master/release" }
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/creativecoinj-core.jar')
    implementation('com.github.tony19:logback-android-classic:1.1.1-5') {
        exclude(group: 'com.google.android', module: 'android')
    }

    //ACTIVE ANDROID
    implementation 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'

    //PIN ACTIVITY
    implementation project(':materialpin')

    //TAGVIEW
    implementation 'com.github.kaedea:Android-Cloud-TagView-Plus:5a49f4fbb6'

    //SETTINGS
    implementation 'com.takisoft.fix:preference-v7:28.0.0.0'

    //HTTP CLIENT
    implementation 'com.github.ander7agar:android-http-client:0.5.0'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.zxing:core:3.3.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'net.glxn.qrgen:android:2.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
}

task svgToMipmapPng(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('src/main/res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

task svgToDrawablePng(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/drawable')
    to = file('src/main/res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'drawable'
}
